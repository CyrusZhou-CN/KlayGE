<?xml version='1.0'?>

<effect>
	<include name="Lighting.fxml"/>
	<include name="util.fxml"/>
	<include name="Quaternion.fxml"/>
	<include name="Mesh.fxml"/>

	<cbuffer name="per_frame">
		<parameter type="float4x4" name="model"/>
		<parameter type="float4x4" name="mvp"/>
		<parameter type="float3" name="eye_pos"/>
		<parameter type="int2" name="diff_spec_mip"/>
		<parameter type="float3" name="diffuse"/>
		<parameter type="float3" name="specular"/>
		<parameter type="float" name="glossiness"/>
	</cbuffer>

	<parameter type="textureCUBE" name="skybox_Ycube_tex"/>
	<parameter type="textureCUBE" name="skybox_Ccube_tex"/>
	<parameter type="sampler" name="skybox_sampler">
		<state name="filtering" value="min_mag_mip_linear"/>
		<state name="address_u" value="clamp"/>
		<state name="address_v" value="clamp"/>
		<state name="address_w" value="clamp"/>
	</parameter>

	<parameter type="texture2D" name="integrated_brdf_tex"/>
	<parameter type="texture2D" name="color_map"/>
	<parameter type="sampler" name="bilinear_sampler">
		<state name="filtering" value="min_mag_linear_mip_point"/>
		<state name="address_u" value="clamp"/>
		<state name="address_v" value="clamp"/>
		<state name="address_w" value="clamp"/>
	</parameter>

	<shader>
		<![CDATA[
float3 PrefilteredDiffuseIBL(float3 c_diff, float3 normal)
{
	float3 prefiltered_clr = decode_hdr_yc(skybox_Ycube_tex.SampleLevel(skybox_sampler, normal, diff_spec_mip.x).r,
		skybox_Ccube_tex.SampleLevel(skybox_sampler, normal, diff_spec_mip.x)).xyz;
	return CalcEnvDiffuse(prefiltered_clr, c_diff);
}

float3 PrefilteredSpecularIBL(float3 c_spec, float glossiness, float3 normal, float3 view)
{
	float mip = CalcPrefilteredEnvMip(glossiness, diff_spec_mip.y);
	float shininess = Glossiness2Shininess(glossiness);
	float3 r = CalcPrefilteredEnvVec(normal, view);
	float3 prefiltered_clr = decode_hdr_yc(skybox_Ycube_tex.SampleLevel(skybox_sampler, r, mip).r,
		skybox_Ccube_tex.SampleLevel(skybox_sampler, r, mip)).xyz;
	return prefiltered_clr * saturate(CalcBRDFShading(0, specular, shininess, r, normal, normal));
}

float3 PBPrefilteredSpecularIBL(float3 c_spec, float glossiness, float3 normal, float3 view)
{
	// Must match the ones in .cpp
	const float LUT_X_SCALE_FACTOR = 1.0079f;
	const float LUT_Y_SCALE_FACTOR = 66.023f;

	float mip = CalcPrefilteredEnvMip(glossiness, diff_spec_mip.y);
	float n_dot_v = saturate(dot(normal, view));
	float3 r = CalcPrefilteredEnvVec(normal, view);	
	float3 prefiltered_clr = decode_hdr_yc(skybox_Ycube_tex.SampleLevel(skybox_sampler, r, mip).r,
		skybox_Ccube_tex.SampleLevel(skybox_sampler, r, mip)).xyz;
	float2 env_brdf = integrated_brdf_tex.Sample(bilinear_sampler, float2(n_dot_v, glossiness)).xy
		/ float2(LUT_X_SCALE_FACTOR, LUT_Y_SCALE_FACTOR);
	return prefiltered_clr * (c_spec * env_brdf.x + env_brdf.y);
}

float3 PBFittingPrefilteredSpecularIBL(float3 c_spec, float glossiness, float3 normal, float3 view)
{
	float mip = CalcPrefilteredEnvMip(glossiness, diff_spec_mip.y);
	float3 r = CalcPrefilteredEnvVec(normal, view);
	float3 prefiltered_clr = decode_hdr_yc(skybox_Ycube_tex.SampleLevel(skybox_sampler, r, mip).r,
		skybox_Ccube_tex.SampleLevel(skybox_sampler, r, mip)).xyz;
	return CalcEnvSpecular(prefiltered_clr, c_spec, glossiness, normal, view);
}

float2 Sigmoid(float2 x)
{
	return 1 / (1 + exp(-x));
}

float3 Sigmoid(float3 x)
{
	return 1 / (1 + exp(-x));
}

float EvalL3Mlp(float2 layer_0,
	float3x2 nn_layer_1_weight, float3 nn_layer_1_bias,
	float3 nn_layer_2_weight, float nn_layer_2_bias)
{
	float3 layer_1;
	layer_1.x = mad(nn_layer_1_weight[0].y, layer_0.y, nn_layer_1_bias.x);
	layer_1.x = mad(nn_layer_1_weight[0].x, layer_0.x, layer_1.x);
	layer_1.y = mad(nn_layer_1_weight[1].y, layer_0.y, nn_layer_1_bias.y);
	layer_1.y = mad(nn_layer_1_weight[1].x, layer_0.x, layer_1.y);
	layer_1.z = mad(nn_layer_1_weight[2].y, layer_0.y, nn_layer_1_bias.z);
	layer_1.z = mad(nn_layer_1_weight[2].x, layer_0.x, layer_1.z);
	layer_1 = Sigmoid(layer_1);

	float layer_2;
	layer_2 = mad(nn_layer_2_weight.z, layer_1.z, nn_layer_2_bias);
	layer_2 = mad(nn_layer_2_weight.y, layer_1.y, layer_2);
	layer_2 = mad(nn_layer_2_weight.x, layer_1.x, layer_2);

	return layer_2;
}

float EvalL4Mlp(float2 layer_0,
	float2x2 nn_layer_1_weight, float2 nn_layer_1_bias,
	float2x2 nn_layer_2_weight, float2 nn_layer_2_bias,
	float2 nn_layer_3_weight, float nn_layer_3_bias)
{
	float2 layer_1;
	layer_1.x = mad(nn_layer_1_weight[0].y, layer_0.y, nn_layer_1_bias.x);
	layer_1.x = mad(nn_layer_1_weight[0].x, layer_0.x, layer_1.x);
	layer_1.y = mad(nn_layer_1_weight[1].y, layer_0.y, nn_layer_1_bias.y);
	layer_1.y = mad(nn_layer_1_weight[1].x, layer_0.x, layer_1.y);
	layer_1 = Sigmoid(layer_1);

	float2 layer_2;
	layer_2.x = mad(nn_layer_2_weight[0].y, layer_1.y, nn_layer_2_bias.x);
	layer_2.x = mad(nn_layer_2_weight[0].x, layer_1.x, layer_2.x);
	layer_2.y = mad(nn_layer_2_weight[1].y, layer_1.y, nn_layer_2_bias.y);
	layer_2.y = mad(nn_layer_2_weight[1].x, layer_1.x, layer_2.y);
	layer_2 = Sigmoid(layer_2);

	float layer_3;
	layer_3 = mad(nn_layer_3_weight.y, layer_2.y, nn_layer_3_bias);
	layer_3 = mad(nn_layer_3_weight.x, layer_2.x, layer_3);

	return layer_3;
}

float3 CalcEnvSpecularMlpNn(float3 prefiltered_env, float3 c_spec, float glossiness, float3 normal, float3 view)
{
	const float2x2 x_nn_layer_1_weight = {
		{1.698710799f, -6.031820297f},
		{5.511096954f, -0.085557237f},
	};
	const float2 x_nn_layer_1_bias = {-2.331894636f, -4.930409431f};

	const float2x2 x_nn_layer_2_weight = {
		{8.675825119f, -4.054215908f},
		{-10.582502365f, -7.232619286f},
	};
	const float2 x_nn_layer_2_bias = {2.326496601f, -4.222003937f};

	const float2 x_nn_layer_3_weight = {-1.277372718f, -31.655321121f};
	const float x_nn_layer_3_bias = 1.582883477f;

	const float3x2 y_nn_layer_1_weight = {
		{8.235061646f, 0.733294487f},
		{-8.698042870f, -1.482306123f},
		{-10.972737312f, 6.122618675f},
	};
	const float3 y_nn_layer_1_bias = {-2.975159407f, 3.277709723f, -3.407575369f};

	const float3 y_nn_layer_2_weight = {-0.128051877f, -0.115933217f, -0.016487861f};
	const float y_nn_layer_2_bias = 0.127637625f;

	float n_dot_v = saturate(dot(normal, view));
	float2 xy = float2(n_dot_v, glossiness);
	float2 env_brdf;
	env_brdf.x = EvalL4Mlp(xy,
		x_nn_layer_1_weight, x_nn_layer_1_bias,
		x_nn_layer_2_weight, x_nn_layer_2_bias,
		x_nn_layer_3_weight, x_nn_layer_3_bias);
	env_brdf.y = EvalL3Mlp(xy,
		y_nn_layer_1_weight, y_nn_layer_1_bias,
		y_nn_layer_2_weight, y_nn_layer_2_bias);

	env_brdf = saturate(env_brdf);
	return prefiltered_env * (c_spec * env_brdf.x + env_brdf.y);
}

float3 PBMlpNnPrefilteredSpecularIBL(float3 c_spec, float glossiness, float3 normal, float3 view)
{
	float mip = CalcPrefilteredEnvMip(glossiness, diff_spec_mip.y);
	float3 r = CalcPrefilteredEnvVec(normal, view);
	float3 prefiltered_clr = decode_hdr_yc(skybox_Ycube_tex.SampleLevel(skybox_sampler, r, mip).r,
		skybox_Ccube_tex.SampleLevel(skybox_sampler, r, mip)).xyz;
	return CalcEnvSpecularMlpNn(prefiltered_clr, c_spec, glossiness, normal, view);
}

float2 ReLU(float2 x)
{
	return max(0, x);
}

float3 BSplines(float input)
{
	const uint GridSize = 1;
	const uint SplineOrder = 2;
	const float Grid[] = {-5, -3, -1,  1,  3,  5};

	float bases[SplineOrder + GridSize + SplineOrder];
	for (uint i = 0; i < SplineOrder + GridSize + SplineOrder; ++i)
	{
		bases[i] = (input >= Grid[i]) && (input < Grid[i + 1]);
	}

	for (uint k = 1; k < SplineOrder + 1; ++k)
	{
		for (uint i = 0; i < SplineOrder + GridSize + SplineOrder - k; ++i)
		{
			bases[i] = (input - Grid[i]) / (Grid[k + i] - Grid[i]) * bases[i] +
						((Grid[k + 1 + i] - input) / (Grid[k + 1 + i] - Grid[1 + i])) * bases[i + 1];
		}
	}

	float3 result;
	[unroll]
	for (i = 0; i < GridSize + SplineOrder; ++i)
	{
		result[i] = bases[i];
	}
	return result;
}

float2 KanForwardL1(float2 input, float2x2 base_weight, float2x3 scaled_spline_weight[2])
{
	float2 activate_in = ReLU(input);
	float3 b_splines_in[2];
	[unroll]
	for (uint i = 0; i < 2; ++i)
	{
		b_splines_in[i] = BSplines(input[i]);
	}

	float2 output = 0;
	[unroll]
	for (i = 0; i < 2; ++i)
	{
		output[i] += dot(activate_in, base_weight[i]);

		[unroll]
		for (uint j = 0; j < 2; ++j)
		{
			output[i] += dot(b_splines_in[j], scaled_spline_weight[i][j]);
		}
	}

	return output;
}

float KanForwardL2(float2 input, float2 base_weight, float2x3 scaled_spline_weight)
{
	float2 activate_in = ReLU(input);
	float3 b_splines_in[2];
	[unroll]
	for (uint i = 0; i < 2; ++i)
	{
		b_splines_in[i] = BSplines(input[i]);
	}

	float output = 0;
	output += dot(activate_in, base_weight);

	[unroll]
	for (uint j = 0; j < 2; ++j)
	{
		output += dot(b_splines_in[j], scaled_spline_weight[j]);
	}

	return output;
}

float EvalL3Kan(float2 layer_0,
	float2x2 kan_layer_1_base_weight, float2x3 kan_layer_1_scaled_spline_weight[2],
	float2 kan_layer_2_base_weight, float2x3 kan_layer_2_scaled_spline_weight)
{
	float2 layer_1 = KanForwardL1(layer_0, kan_layer_1_base_weight, kan_layer_1_scaled_spline_weight);
	return KanForwardL2(layer_1, kan_layer_2_base_weight, kan_layer_2_scaled_spline_weight);
}

float3 CalcEnvSpecularKanNn(float3 prefiltered_env, float3 c_spec, float glossiness, float3 normal, float3 view)
{
	const float2x2 x_kan_layer_1_base_weight = {
		{-1.533318877f, -0.177919418f},
		{0.667444229f, -2.907330751f},
	};
	const float2x3 x_kan_layer_1_scaled_spline_weight[] = {
		{
			{3.340777397f, 0.438279629f, -0.716442466f},
			{13.258321762f, 0.444858730f, 1.149049997f},
		},
		{
			{-8.731234550f, -0.195254281f, 1.023126364f},
			{-12.617908478f, 2.421995163f, -6.325132370f},
		},
	};

	const float2 x_kan_layer_2_base_weight = {0.049643919f, -0.160301566f};
	const float2x3 x_kan_layer_2_scaled_spline_weight = {
		{1.749079704f, 0.081547886f, -0.173867196f},
		{0.059698336f, 0.427082509f, 0.281334043f},
	};

	const float2x2 y_kan_layer_1_base_weight = {
		{0.540755570f, -0.293241560f},
		{-0.822410166f, 0.321761012f},
	};
	const float2x3 y_kan_layer_1_scaled_spline_weight[] = {
		{
			{10.003301620f, -2.391380548f, 2.107568502f},
			{1.743881464f, -0.316108942f, 0.455026537f},
		},
		{
			{-6.832652569f, 1.835685968f, -5.013943195f},
			{-3.251152992f, 0.435529113f, -0.533867478f},
		},
	};

	const float2 y_kan_layer_2_base_weight = {-0.015559956f, -0.063676387f};
	const float2x3 y_kan_layer_2_scaled_spline_weight = {
		{-0.076168276f, -0.244602069f, 0.214840800f},
		{0.183108643f, 0.186333150f, 0.238902688f},
	};

	float n_dot_v = saturate(dot(normal, view));
	float2 xy = float2(n_dot_v, glossiness);
	float2 env_brdf;
	env_brdf.x = EvalL3Kan(xy,
		x_kan_layer_1_base_weight, x_kan_layer_1_scaled_spline_weight,
		x_kan_layer_2_base_weight, x_kan_layer_2_scaled_spline_weight);
	env_brdf.y = EvalL3Kan(xy,
		y_kan_layer_1_base_weight, y_kan_layer_1_scaled_spline_weight,
		y_kan_layer_2_base_weight, y_kan_layer_2_scaled_spline_weight);

	env_brdf = saturate(env_brdf);
	return prefiltered_env * (c_spec * env_brdf.x + env_brdf.y);
}

float3 PBKanNnPrefilteredSpecularIBL(float3 c_spec, float glossiness, float3 normal, float3 view)
{
	float mip = CalcPrefilteredEnvMip(glossiness, diff_spec_mip.y);
	float3 r = CalcPrefilteredEnvVec(normal, view);
	float3 prefiltered_clr = decode_hdr_yc(skybox_Ycube_tex.SampleLevel(skybox_sampler, r, mip).r,
		skybox_Ccube_tex.SampleLevel(skybox_sampler, r, mip)).xyz;
	return CalcEnvSpecularKanNn(prefiltered_clr, c_spec, glossiness, normal, view);
}

void GroundTruthVS(float4 pos		: POSITION,
				float4 tangent_quat	: TANGENT,
				out float3 oNormal	: TEXCOORD0,
				out float3 oIncident : TEXCOORD1,
				out float4 oPos		: SV_Position)
{
	pos = float4(pos.xyz * pos_extent + pos_center, 1);
	tangent_quat = tangent_quat * 2 - 1;

	oPos = mul(pos, mvp);
	oNormal = mul(transform_quat(float3(0, 0, 1), tangent_quat), (float3x3)model);
	oIncident = eye_pos - mul(pos, model).xyz;
}

float4 PrefilteredPS(float3 normal : TEXCOORD0, float3 incident : TEXCOORD1) : SV_Target
{
	normal = normalize(normal);
	incident = normalize(incident);

	return float4(PrefilteredDiffuseIBL(diffuse, normal)
		+ PrefilteredSpecularIBL(specular, glossiness, normal, incident), 1);
}

float4 PBPrefilteredPS(float3 normal : TEXCOORD0, float3 incident : TEXCOORD1) : SV_Target
{
	normal = normalize(normal);
	incident = normalize(incident);

	return float4(PrefilteredDiffuseIBL(diffuse, normal)
		+ PBPrefilteredSpecularIBL(specular, glossiness, normal, incident), 1);
}

float4 PBFittingPrefilteredPS(float3 normal : TEXCOORD0, float3 incident : TEXCOORD1) : SV_Target
{
	normal = normalize(normal);
	incident = normalize(incident);

	return float4(PrefilteredDiffuseIBL(diffuse, normal)
		+ PBFittingPrefilteredSpecularIBL(specular, glossiness, normal, incident), 1);
}

float4 PBMlpNnPrefilteredPS(float3 normal : TEXCOORD0, float3 incident : TEXCOORD1) : SV_Target
{
	normal = normalize(normal);
	incident = normalize(incident);

	return float4(PrefilteredDiffuseIBL(diffuse, normal)
		+ PBMlpNnPrefilteredSpecularIBL(specular, glossiness, normal, incident), 1);
}

float4 PBKanNnPrefilteredPS(float3 normal : TEXCOORD0, float3 incident : TEXCOORD1) : SV_Target
{
	normal = normalize(normal);
	incident = normalize(incident);

	return float4(PrefilteredDiffuseIBL(diffuse, normal)
		+ PBKanNnPrefilteredSpecularIBL(specular, glossiness, normal, incident), 1);
}

float4 ShowErrorPS(float3 normal : TEXCOORD0, float3 incident : TEXCOORD1) : SV_Target
{
#if METHOD == 0
	float4 prefiltered = PBFittingPrefilteredPS(normal, incident);
#elif METHOD == 1
	float4 prefiltered = PBMlpNnPrefilteredPS(normal, incident);
#elif METHOD == 2
	float4 prefiltered = PBKanNnPrefilteredPS(normal, incident);
#endif

	float3 error = abs(prefiltered - PBPrefilteredPS(normal, incident)).xyz;

	const float3 RGB_TO_LUM = float3(0.2126f, 0.7152f, 0.0722f);
	float lum = saturate(dot(error * 20, RGB_TO_LUM));

	return pow(color_map.SampleLevel(bilinear_sampler, float2(lum, 0.5f), 0), 2.2f);
}
		]]>
	</shader>

	<shader version="4">
		<![CDATA[
uint ReverseBits(uint bits)
{
	bits = (bits << 16) | (bits >> 16);
	bits = ((bits & 0x55555555) << 1) | ((bits & 0xAAAAAAAA) >> 1);
	bits = ((bits & 0x33333333) << 2) | ((bits & 0xCCCCCCCC) >> 2);
	bits = ((bits & 0x0F0F0F0F) << 4) | ((bits & 0xF0F0F0F0) >> 4);
	bits = ((bits & 0x00FF00FF) << 8) | ((bits & 0xFF00FF00) >> 8);
	return bits;
}

float RadicalInverseVdC(uint bits)
{
	return ReverseBits(bits) * 2.3283064365386963e-10f; // / 0x100000000
}

float2 Hammersley2D(uint i, uint N)
{
	return float2(float(i) / N, RadicalInverseVdC(i));
}

float3 ImportanceSampleLambert(float2 xi)
{
	const float PI = 3.1415926f;

	float phi = 2 * PI * xi.x;
	float cos_theta = sqrt(1 - xi.y);
	float sin_theta = sqrt(1 - cos_theta * cos_theta);
	return float3(sin_theta * cos(phi), sin_theta * sin(phi), cos_theta);
}

float3 ImportanceSampleLambert(float2 xi, float3 normal)
{
	float3 h = ImportanceSampleLambert(xi);
	
	float3 up_vec = abs(normal.z) < 0.999f ? float3(0, 0, 1) : float3(1, 0, 0);
	float3 tangent = normalize(cross(up_vec, normal));
	float3 binormal = cross(normal, tangent);
	return tangent * h.x + binormal * h.y + normal * h.z;
}

float3 ImportanceSampleBP(float2 xi, float shininess)
{
	const float PI = 3.1415926f;

	float phi = 2 * PI * xi.x;
	float cos_theta = pow(1 - xi.y * (shininess + 1) / (shininess + 2), 1 / (shininess + 1));
	float sin_theta = sqrt(1 - cos_theta * cos_theta);
	return float3(sin_theta * cos(phi), sin_theta * sin(phi), cos_theta);
}

float3 ImportanceSampleBP(float2 xi, float shininess, float3 normal)
{
	float3 h = ImportanceSampleBP(xi, shininess);
	
	float3 up_vec = abs(normal.z) < 0.999f ? float3(0, 0, 1) : float3(1, 0, 0);
	float3 tangent = normalize(cross(up_vec, normal));
	float3 binormal = cross(normal, tangent);
	return tangent * h.x + binormal * h.y + normal * h.z;
}

float GImplicit(float n_dot_v, float n_dot_l)
{
	return n_dot_v * n_dot_l;
}

float3 DiffuseIBL(float3 c_diff, float3 normal)
{
	float3 prefiltered_clr = 0;
	
	const uint NUM_SAMPLES = 1024;
	for (uint i = 0; i < NUM_SAMPLES; ++ i)
	{
		float2 xi = Hammersley2D(i, NUM_SAMPLES);
		float3 l = ImportanceSampleLambert(xi, normal);
		prefiltered_clr += decode_hdr_yc(skybox_Ycube_tex.SampleLevel(skybox_sampler, l, 0).r,
			skybox_Ccube_tex.SampleLevel(skybox_sampler, l, 0)).xyz;
	}

	return c_diff * prefiltered_clr / NUM_SAMPLES;
}

float3 SpecularIBL(float3 c_spec, float glossiness, float3 normal, float3 view)
{
	float shininess = Glossiness2Shininess(glossiness);

	float3 specular = 0;
	
	const uint NUM_SAMPLES = 1024;
	for (uint i = 0; i < NUM_SAMPLES; ++ i)
	{
		float2 xi = Hammersley2D(i, NUM_SAMPLES);
		float3 h = ImportanceSampleBP(xi, shininess, normal);
		float3 l = -reflect(view, h);
		float n_dot_v = saturate(dot(normal, view));
		float n_dot_l = saturate(dot(normal, l));
		float n_dot_h = saturate(dot(normal, h));
		float v_dot_h = saturate(dot(view, h));
		if (n_dot_l > 0)
		{
			float3 env_clr = decode_hdr_yc(skybox_Ycube_tex.SampleLevel(skybox_sampler, l, 0).r,
				skybox_Ccube_tex.SampleLevel(skybox_sampler, l, 0)).xyz;
			float g = GImplicit(n_dot_v, n_dot_l);
			float fc = pow(1 - v_dot_h, 5);
			float3 f = (1 - fc) * c_spec + fc;
			specular += env_clr * f * g * v_dot_h / max(1e-6f, n_dot_h * n_dot_v);
		}
	}

	return specular / NUM_SAMPLES;
}

float3 PrefilterEnvMap(float shininess, float3 r)
{
	float3 normal = r;
	float3 view = r;
	float3 prefiltered_clr = 0;
	float total_weight = 0;

	const uint NUM_SAMPLES = 1024;
	for (uint i = 0; i < NUM_SAMPLES; ++ i)
	{
		float2 xi = Hammersley2D(i, NUM_SAMPLES);
		float3 h = ImportanceSampleBP(xi, shininess, normal);
		float3 l = -reflect(view, h);
		float n_dot_l = saturate(dot(normal, l));
		if (n_dot_l > 0)
		{
			prefiltered_clr += decode_hdr_yc(skybox_Ycube_tex.SampleLevel(skybox_sampler, l, 0).r,
				skybox_Ccube_tex.SampleLevel(skybox_sampler, l, 0)).xyz * n_dot_l;
			total_weight += n_dot_l;
		}
	}

	return prefiltered_clr / max(1e-6f, total_weight);
}

float2 IntegrateBRDF(float shininess, float n_dot_v)
{
	float3 view = float3(sqrt(1 - n_dot_v * n_dot_v), 0, n_dot_v);
	float2 rg = 0;

	const uint NUM_SAMPLES = 1024;
	for (uint i = 0; i < NUM_SAMPLES; ++ i)
	{
		float2 xi = Hammersley2D(i, NUM_SAMPLES);
		float3 h = ImportanceSampleBP(xi, shininess);
		float3 l = -reflect(view, h);
		float n_dot_l = saturate(l.z);
		float n_dot_h = saturate(h.z);
		float v_dot_h = saturate(dot(view, h));
		if (n_dot_l > 0)
		{
			float g = GImplicit(n_dot_v, n_dot_l);
			float g_vis = g * v_dot_h / max(1e-6f, n_dot_h * n_dot_v);
			float fc = pow(1 - v_dot_h, 5);
			rg += float2(1 - fc, fc) * g_vis;
		}
	}

	return rg / NUM_SAMPLES;
}

float3 ApproximateSpecularIBL(float3 c_spec, float glossiness, float3 normal, float3 view)
{
	float shininess = Glossiness2Shininess(glossiness);

	float n_dot_v = saturate(dot(normal, view));
	float3 r = -reflect(view, normal);
	float3 prefiltered_clr = PrefilterEnvMap(shininess, r);
	float2 env_brdf = IntegrateBRDF(shininess, n_dot_v);
	return prefiltered_clr * (c_spec * env_brdf.x + env_brdf.y);
}

float4 GroundTruthPS(float3 normal : TEXCOORD0, float3 incident : TEXCOORD1) : SV_Target
{
	normal = normalize(normal);
	incident = normalize(incident);

	return float4(DiffuseIBL(diffuse, normal)
		+ SpecularIBL(specular, glossiness, normal, incident), 1);
}

float4 ApproximatePS(float3 normal : TEXCOORD0, float3 incident : TEXCOORD1) : SV_Target
{
	normal = normalize(normal);
	incident = normalize(incident);

	return float4(DiffuseIBL(diffuse, normal)
		+ ApproximateSpecularIBL(specular, glossiness, normal, incident), 1);
}
		]]>
	</shader>

	<technique name="GroundTruth">
		<pass name="p0">
			<state name="vertex_shader" value="GroundTruthVS()"/>
			<state name="pixel_shader" value="GroundTruthPS()"/>
		</pass>
	</technique>
	<technique name="Approximate">
		<pass name="p0">
			<state name="vertex_shader" value="GroundTruthVS()"/>
			<state name="pixel_shader" value="ApproximatePS()"/>
		</pass>
	</technique>
	<technique name="Prefiltered">
		<pass name="p0">
			<state name="vertex_shader" value="GroundTruthVS()"/>
			<state name="pixel_shader" value="PrefilteredPS()"/>
		</pass>
	</technique>
	<technique name="PBPrefiltered">
		<pass name="p0">
			<state name="vertex_shader" value="GroundTruthVS()"/>
			<state name="pixel_shader" value="PBPrefilteredPS()"/>
		</pass>
	</technique>
	<technique name="PBFittingPrefiltered">
		<pass name="p0">
			<state name="vertex_shader" value="GroundTruthVS()"/>
			<state name="pixel_shader" value="PBFittingPrefilteredPS()"/>
		</pass>
	</technique>
	<technique name="PBFittingError">
		<pass name="p0">
			<macro name="METHOD" value="0"/>

			<state name="vertex_shader" value="GroundTruthVS()"/>
			<state name="pixel_shader" value="ShowErrorPS()"/>
		</pass>
	</technique>
	<technique name="PBMlpNnPrefiltered">
		<pass name="p0">
			<state name="vertex_shader" value="GroundTruthVS()"/>
			<state name="pixel_shader" value="PBMlpNnPrefilteredPS()"/>
		</pass>
	</technique>
	<technique name="PBMlpNnError">
		<pass name="p0">
			<macro name="METHOD" value="1"/>

			<state name="vertex_shader" value="GroundTruthVS()"/>
			<state name="pixel_shader" value="ShowErrorPS()"/>
		</pass>
	</technique>
	<technique name="PBKanNnPrefiltered">
		<pass name="p0">
			<state name="vertex_shader" value="GroundTruthVS()"/>
			<state name="pixel_shader" value="PBKanNnPrefilteredPS()"/>
		</pass>
	</technique>
	<technique name="PBKanNnError">
		<pass name="p0">
			<macro name="METHOD" value="2"/>

			<state name="vertex_shader" value="GroundTruthVS()"/>
			<state name="pixel_shader" value="ShowErrorPS()"/>
		</pass>
	</technique>
</effect>
